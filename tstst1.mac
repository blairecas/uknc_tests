	.title	MAIN
	.mcall	.print .exit
	.enabl	LC ; without AMA, contains relocatable code
	
	.asect
	.=1000

Start:
	mov	#176640, R4
	mov	#176642, R5
	mov	#100000, (R4)

	; LFSR
	mov	#80.*60., R3	
10$:	call	Random
	mov	R0, (R5)
	inc	(R4)
	sob	R3, 10$
	call	SomeLine

	; C64
;	mov	#80.*60., R3	
;20$:	call	NextRandom
;	clr	R0
;	bisb	RandSeed1, R0
;	swab	R0
;	bisb	RandSeed2, R0	
;	mov	R0, (R5)
;	inc	(R4)
;	sob	R3, 20$

	; Nikita's
	mov	#80.*60., R1
20$:	call	LA270
	mov	R3, (R5)
	inc	(R4)
	sob	R1, 20$
	call	SomeLine

	; Manwe's
	mov	#80.*60., R3	
30$:	call	RND
	mov	R0, (R5)
	inc	(R4)
	sob	R3, 30$
	call	SomeLine

	; xorshift
	mov	#80.*60., R2	
40$:	call	XorShift
	mov	R0, (R5)
	inc	(R4)
	sob	R2, 40$

	br .

; some line
SomeLine:
	add	#160., (R4)
	return

; xorshift
XorShift:
	mov	(PC)+, R0
Xor1:	.word	12345
	; xs ^= xs << 1
 	mov r0, r1
 	asl   r1
 	xor r1, r0
 	; xs ^= xs >> 15
  	sxt r1 ; use N flag (bit15 after xor)
 	neg  r1
 	xor r1, r0
 	; xs ^= xs >> 1
 	mov r0, r1
 	clc
 	ror  r1
 	xor r1, r0
	mov R0, Xor1
	return

; Routine: Get pseudo-random number, result in HL
; Портит: R0, R2, R3
; Результат в R3
LA270:
  MOV  L5C76, R2  ; A270  LD DE,($5C76)  ; Read RND SEED variable
  MOV  R2, R3    ; A274  LD H,E
  SWAB  R3
  BIC  #377, R3
  ADD  #375, R3  ; A275  LD L,$FD
  MOV  R2, R0    ; A277  LD A,D
  SWAB  R0
  BIC  #177400, R0  ; A278  OR A
  SUB  R2, R3    ; A279  SBC HL,DE
  SBCB  R0    ; A27B  SBC A,$00
  SUB  R2, R3    ; A27D  SBC HL,DE
  SBCB  R0    ; A27F  SBC A,$00
        ; A281  LD E,A
  BIC  #177400, R0  ; A282  LD D,$00
  SUB  R0, R3    ; A284  SBC HL,DE
  BHIS  LA289    ; A286  JR NC,$A289
  INC  R3    ; A288  INC HL
LA289:  MOV  R3, L5C76  ; A289  LD ($5C76),HL  ; Write RND SEED variable
  RETURN      ; A28C  RET
L5C76:  .WORD  123456  ; Pseudo-random seed

; Sandro's
crand:
  mov (pc)+, r3
crand_a:
  .word 0
  mov (pc)+, r0
crand_b:
  .word 0
  add #53731., r3
  add r3, r0
  mov r0, @#crand_b
  clrb r0
  swab r0
  add r0, r3
  mov r3, @#crand_a
  mov r0, 2(sp)
  return

; Manwe's
RND:	MOV	(PC)+,R0
SEED1:	.WORD	173451
	MOV	(PC)+,R1
SEED2:	.WORD	54102
	ROR	R0
	ROL	R1
	SWAB	R1
	XOR	R1,R0
	MOV	R0,SEED1
	MOV	R1,SEED2
	RETURN

; LFSR
Random: clc
	ror  (PC)+
Lfsr00: .word  126341     ; 0ACE1
	bcc  Lfsr03
	mov  (PC)+, R0
Lfsr01: .word  132000    ; 0B400
	xor  R0, Lfsr00
Lfsr03: mov  Lfsr00, R0
	return

RandSeed1:	.word	0
RandSeed2:	.word	0
TempRand1:	.word	0
TempRand2:	.word	0

; Random from C64 version
NextRandom:
	; set TempRand1
	mov	RandSeed1, R0			; LDA RandSeed1
	rorb	R0				; ROR
        rorb	R0				; ROR
        bic	#^B1111111101111111, R0		; AND #$80
        movb	R0, TempRand1			; STA TempRand1
	; set TempRand2
	mov	RandSeed2, R0			; LDA RandSeed2
	rorb	R0				; ROR
	bic	#^B1111111110000000, R0		; AND #$7F
	movb	R0, TempRand2			; STA TempRand2
	;
	movb	RandSeed2, R0			; LDA RandSeed2
	rorb	R0				; ROR
	rorb	R0				; ROR
	bic	#^B1111111101111111, R0		; AND #$80
						; CLC
	add	RandSeed2, R0			; ADC RandSeed2
	cmp	#377, R0			; *** put C flag - cause we havent ADDB
	bic	#^B1111111100000000, R0		; *** C flag not changing with BIC
	adc	R0				; 
	add	#23, R0				; ADC #0x13
	cmp	#377, R0			; *** again put C flag, but now not need to clean upper
	movb	R0, RandSeed2			; STA RandSeed2
	mov	RandSeed1, R0			; LDA RandSeed1
	adc	R0				;
	add	TempRand1, R0			; ADC TempRand1
	cmp	#377, R0			;
	adc	R0				;
	add	TempRand2, R0			; ADC TempRand2
	movb	R0, RandSeed1			; STA RandSeed1
	return

	.end	Start
